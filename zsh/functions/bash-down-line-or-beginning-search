# Like down-line-or-search, but uses the whole line prefix up to the
# cursor position for searching forwards.  Moves cursor to the end of the
# line if you start a search without a prefix, just like Bash.

emulate -L zsh

if [[ ${+NUMERIC} -eq 0 &&
    ( $LASTWIDGET = $__searching || $RBUFFER != *$'\n'* ) ]]
then
    [[ $LASTWIDGET = $__searching ]] && CURSOR=$__savecursor
    __searching=$WIDGET
    __savecursor=$CURSOR
    if zle .history-beginning-search-forward; then
        [[ $RBUFFER = *$'\n'* ]] ||
            zstyle -T ':zle:bash-down-line-or-beginning-search' leave-cursor &&
            [[ $__savecursor = "0" ]] && zle .end-of-line
        return
    fi
    [[ $RBUFFER = *$'\n'* ]] || return
fi
__searching=''
zle .down-line-or-history
